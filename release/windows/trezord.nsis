!include MUI2.nsh
!include x64.nsh
!include LogicLib.nsh

RequestExecutionLevel admin

SetCompressor bzip2

Name "TREZOR Bridge"
InstallDirRegKey HKLM Software\TREZOR\Bridge InstallDir

ShowInstDetails hide
ShowUninstDetails hide

XPStyle on

Page directory
Page instfiles

DirText "Please select the installation folder."

Section "TREZOR Bridge"
  SectionIn RO

  DetailPrint "Stopping previous TREZOR Bridge"
  nsExec::Exec "taskkill /IM trezord.exe /F"

  SetOutPath "$INSTDIR"
  ${If} ${RunningX64}
    File /oname=wdi-simple.exe wdi-simple-64b.exe
    File /oname=trezord.exe trezord-64b.exe
    File /oname=devcon.exe devcon-64b.exe
  ${Else}
    File /oname=wdi-simple.exe wdi-simple-32b.exe
    File /oname=trezord.exe trezord-32b.exe
    File /oname=devcon.exe devcon-32b.exe
  ${EndIf}
  File "trezord.ico"
SectionEnd

Section "Start Menu Shortcuts"
  CreateDirectory "$SMPROGRAMS\TREZOR Bridge"
  CreateShortCut "$SMPROGRAMS\TREZOR Bridge\Uninstall.lnk" "$INSTDIR\Uninstall.exe" "" "$INSTDIR\Uninstall.exe" 0
  CreateShortCut "$SMPROGRAMS\TREZOR Bridge\TREZOR Bridge.lnk" "$INSTDIR\trezord.exe" "-l $\"%APPDATA%\TREZOR Bridge\trezord.log$\"" "$INSTDIR\trezord.ico" 0
  CreateShortCut "$SMSTARTUP\TREZOR Bridge.lnk" "$INSTDIR\trezord.exe" "-l $\"%APPDATA%\TREZOR Bridge\trezord.log$\"" "$INSTDIR\trezord.ico" 0
SectionEnd

Section "Install drivers"
  DetailPrint "Running $INSTDIR\wdi-simple.exe"
  nsExec::ExecToLog '"$INSTDIR\wdi-simple.exe" --name "TREZOR" --manufacturer "SatoshiLabs" --vid 0x1209 --pid 0x53C0 --progressbar=$HWNDPARENT'
  nsExec::ExecToLog '"$INSTDIR\wdi-simple.exe" --name "TREZOR" --manufacturer "SatoshiLabs" --vid 0x1209 --pid 0x53C1 --iid 0 --progressbar=$HWNDPARENT'
SectionEnd

Section "Uninstall"
  ExecWait "taskkill /f /im trezord.exe"

  Delete /rebootok "$SMSTARTUP\TREZOR Bridge.lnk"
  Delete /rebootok "$SMPROGRAMS\TREZOR Bridge\TREZOR Bridge.lnk"
  Delete /rebootok "$SMPROGRAMS\TREZOR Bridge\Uninstall.lnk"
  RMDir "$SMPROGRAMS\TREZOR Bridge"

  RMDir /r /rebootok "$INSTDIR\usb_driver"
  Delete /rebootok "$INSTDIR\devcon.exe"
  Delete /rebootok "$INSTDIR\wdi-simple.exe"
  Delete /rebootok "$INSTDIR\trezord.exe"
  Delete /rebootok "$INSTDIR\trezord.ico"
  Delete /rebootok "$INSTDIR\Uninstall.exe"
  RMDir "$INSTDIR"
SectionEnd

Section -post
  WriteUninstaller "$INSTDIR\Uninstall.exe"
SectionEnd

!define MUI_FINISHPAGE_RUN
!define MUI_FINISHPAGE_RUN_TEXT "Start TREZOR Bridge"
!define MUI_FINISHPAGE_RUN_FUNCTION "LaunchApplication"
!insertmacro MUI_PAGE_FINISH

Function LaunchApplication
  ExecShell "" "$SMPROGRAMS\TREZOR Bridge\TREZOR Bridge.lnk"
FunctionEnd
